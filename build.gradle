/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'enisinanaj' at '11/13/16 8:40 PM' with Gradle 2.14.1
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.14.1/userguide/tutorial_java_projects.html
 */


/************* BUILD CONFIGURATION ***************/

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'org.akhikhl.gretty:gretty:2.0.0'
		classpath 'org.hibernate:hibernate-gradle-plugin:5.2.11.Final'
	}
}

// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'
apply plugin: 'org.hibernate.orm'

hibernate {
	enhance {
		enableDirtyTracking = true
	}
}

gretty {
	httpPort = 8000
	contextPath = '/teamtable'
	fastReload = true
	// ... many more properties
}

/************* RUNTIME CONFIGURATION ***************/

war.archiveName "teamtable.war"

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
	mavenCentral()
    jcenter()
}

def jersey_version = 2.24

// In this section you declare the dependencies for your production and test code
dependencies {
    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.21'

	compile group: 'org.jboss.spec', name: 'jboss-javaee-7.0', version:'1.0.3.Final'

	// groovy
	compile 'org.codehaus.groovy:groovy-all:2.4.7'

	// rest dependencies
	compile 'javax.ws.rs:javax.ws.rs-api:2.0.1'
	compile 'org.glassfish.jersey.containers:jersey-container-servlet:' + jersey_version
	compile group: 'org.glassfish.jersey.media', name: 'jersey-media-moxy', version: jersey_version
	compile group: 'org.glassfish.jersey.ext', name: 'jersey-entity-filtering', version: jersey_version
	
	// id scrambling dependency
	compile 'org.hashids:hashids:1.0.1'
	compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
	compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
	compile group: 'commons-codec', name: 'commons-codec', version: '1.11'

	// database
    compile 'javax.el:el-api:2.2'
    compile 'mysql:mysql-connector-java:5.1.18'
	compile 'org.hibernate:hibernate-core:5.2.11.Final'
	compile group: 'org.hibernate', name: 'hibernate-c3p0', version: '5.2.11.Final'

	compile 'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final'
	compile 'org.hibernate:hibernate-entitymanager:5.2.11.Final'

    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.
    testCompile 'junit:junit:4.12'
	testCompile "org.mockito:mockito-core:2.+"
}

task deploygf(type: Copy) {
    from "${buildDir}/libs/teamtable.war"
    into "/usr/local/Cellar/glassfish/5.0/libexec/glassfish/domains/domain1/autodeploy/"
}

deploygf.dependsOn war
deploygf.doFirst {
    println "Copying to autodeploy dir"
}

defaultTasks "deploygf"

tasks.withType(Test) {
	testLogging {
		exceptionFormat "full"
		events "skipped", "passed", "failed"
		showStandardStreams true
	}
}